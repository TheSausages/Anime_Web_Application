plugins {
    id "com.diffplug.spotless" version "6.20.0"
}

subprojects { sub ->
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    group = 'anime.app'

    java {
        sourceCompatibility = '17'
    }

    spotless {
        encoding 'UTF-8'
        enforceCheck true

        java {
            googleJavaFormat('1.17.0')

            importOrder()
            removeUnusedImports()
            formatAnnotations()
            toggleOffOn()
            endWithNewline()

            // Versions 3.x.x throw na error, so downgrade is needed
            prettier(['prettier': '2.8.8', 'prettier-plugin-java': '2.2.0'])
                    .config(['parser': 'java', 'tabWidth': 4])
        }

        typescript {
            toggleOffOn()
            target "${buildDir}/generated/src/typescript"

            tsfmt('7.2.2')
                    .config(['indentSize': 4, 'convertTabsToSpaces': true])
            endWithNewline()

            prettier('prettier': '2.8.8').config(['parser': 'java', 'tabWidth': 4])
        }

        format 'misc', {
            target '*.gradle', '*.md', '.gitignore'

            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
        }
    }

    // Copy the realm to each service so the integration tests will run
    tasks.register('copyKeycloakRealm', Copy) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        from "${rootDir}${File.separator}docker${File.separator}keycloak${File.separator}realm-export.json"
        into "${projectDir}/src/test/resources"
    }

    processTestResources {
        dependsOn copyKeycloakRealm
    }
}