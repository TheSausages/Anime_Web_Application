plugins {
    id "com.diffplug.spotless" version "6.20.0"
}

subprojects { sub ->
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    group = 'anime.app'

    java {
        sourceCompatibility = '17'
    }

    // Copy the realm so the integration tests will run
    tasks.register('copyKeycloakRealm', Copy) {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
        from "${rootDir}${File.separator}docker${File.separator}keycloak${File.separator}realm-export.json"
        into "${projectDir}/src/test/resources"
    }

    processTestResources {
        dependsOn copyKeycloakRealm
    }

    //Format the spotless prettier
    spotless {
        encoding 'UTF-8'
        enforceCheck true

        java {
            googleJavaFormat()

            importOrder()
            removeUnusedImports()
            formatAnnotations()
            toggleOffOn()

            //Use the prettier to format the generated classes
            prettier(['prettier': '3.0.2', 'prettier-plugin-java': '2.2.0'])
                    .config(['parser': 'java', 'tabWidth': 4])
        }
    }

}