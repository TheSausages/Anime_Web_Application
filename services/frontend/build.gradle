plugins {
    id "com.github.node-gradle.node" version "7.0.1"
}

version = "0.0.1-SNAPSHOT"

task copyGeneratedFrontendCode(type: Copy) {
    dependsOn ":API_Generation:generateTypescriptCode"
    dependsOn ":API_Generation:spotlessTypescript"

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from "${rootDir}/services/common/API_Generation/build/generated/typescript/models"
    into "${projectDir}/src/generated"
}

task formatFrontendCode(type: NpxTask) {
    dependsOn npmInstall
    dependsOn copyGeneratedFrontendCode
    dependsOn spotlessMiscApply

    command = 'npm'
    args = ['run', 'format']
}

task buildFrontend(type: NpxTask) {
    dependsOn formatFrontendCode

    command = 'npm'
    args = ['run', 'build']

    inputs.files('package.json', 'package-lock.json', 'index.html', 'tsconfig.json', 'tsconfig.node.json', 'vite.config.ts')
    inputs.dir('src')
    inputs.dir('public')
    inputs.dir(fileTree("node_modules").exclude(".cache"))
    outputs.dir('dist')

    build.dependsOn(buildFrontend)
    spotlessSql.dependsOn(buildFrontend)
}

task runFrontendTests(type: NpxTask) {
    dependsOn formatFrontendCode

    command = 'npm'
    args = ['run', 'test']

    test.dependsOn(runFrontendTests)
}

task runFrontendLocally(type: NpxTask) {
    dependsOn formatFrontendCode

    command = 'npm'
    args = ['run', 'dev']
}

node {
    download = false
}
